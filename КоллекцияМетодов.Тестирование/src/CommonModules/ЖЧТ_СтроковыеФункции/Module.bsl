
#Область СлужебныйПрограммныйИнтерфейс

// Регистрация тестов
Процедура ИсполняемыеСценарии() Экспорт

	Перем ЮТТесты;
	ЮТТесты = ЖЧТ_Движок.ЮТТесты();
	
	ЮТТесты
		.ДобавитьТестовыйНабор("ВставитьПараметрыВСтроку")
			.ДобавитьТест("Тест_ВставитьПараметрыВСтроку_ПараметрыВнутри", "Параметры внутри шаблона строки")
			.ДобавитьТест("Тест_ВставитьПараметрыВСтроку_ПараметрВНачале", "Параметр сразу в начале шаблона")
			.ДобавитьТест("Тест_ВставитьПараметрыВСтроку_ПараметрВКонце", "Параметр в конце шаблона")
			.ДобавитьТест("Тест_ВставитьПараметрыВСтроку_ТолькоПараметр", "В шаблоне только параметр")
			.ДобавитьТест("Тест_ВставитьПараметрыВСтроку_Ошибка_НетСвойства", "Ошибка, если нет свойства")
			.ДобавитьТест("Тест_ВставитьПараметрыВСтроку_Ошибка_НомерныеПараметры", "Ошибка, если передано %N")
			.ДобавитьТест("Тест_ВставитьПараметрыВСтроку_Ошибка_НеверныйФормат", "Ошибка, если передано %%")
		.ДобавитьТестовыйНабор("СтрокаВыделеннаяОформлением")
			.ДобавитьТест("Тест_СтрокаВыделеннаяОформлением_Начало", "Выделено первое слово")
			.ДобавитьТест("Тест_СтрокаВыделеннаяОформлением_Середина", "Выделено среднее слово")
			.ДобавитьТест("Тест_СтрокаВыделеннаяОформлением_Конец", "Выделено последнее слово")
			.ДобавитьТест("Тест_СтрокаВыделеннаяОформлением_Множество", "Выделено множество букв внутри строки")
		.ДобавитьТестовыйНабор("СтрокаНайтиИВыделитьОформлением")
			.ДобавитьТест("Тест_СтрокаНайтиИВыделитьОформлением")
				.СПараметрами("Привет", "1", Неопределено)
				.СПараметрами("Привет", "ив", "Пр`ив`ет")
				.СПараметрами("CNT-00230", "23", "CNT-00`23`0")
				.СПараметрами("CNT-00230", "0", "CNT-`00`23`0`")
		.ДобавитьТестовыйНабор("ШаблонСтрокиКорректен")
			.ДобавитьТест("Тест_ШаблонСтрокиКорректен")
				.СПараметрами(""		, Истина, "Пустая строка")
				.СПараметрами("%1"		, Истина, "Один параметр")
				.СПараметрами("%%%1"	, Истина, "Экранирование % и параметр")
				.СПараметрами("%(1)2"	, Истина, "Экранирование номера параметра")
				.СПараметрами("%(1)%2"	, Истина, "Экранирование номера параметра + второй параметр")
				.СПараметрами("%2"		, Истина, "Только второй параметр")
				.СПараметрами("%2%9"	, Истина, "Второй и девятый параметр")
				.СПараметрами("%11"		, Ложь  , "Параметр > 10")
				.СПараметрами("%((1))"	, Ложь  , "Двойные скобки номера параметра")
				.СПараметрами("%"		, Ложь  , "% без номера параметра")
				.СПараметрами("%(11)"	, Ложь  , "Экранированный параметр > 10")
				.СПараметрами("%-1"		, Ложь  , "Отрицательный пареметр")
				.СПараметрами("%(-1)"	, Ложь  , "Экранированный отрицательный пареметр")
		.ДобавитьТестовыйНабор("СтрокаСимволов")
			.ДобавитьТест("Тест_СтрокаСимволов")
				.СПараметрами("1", 5, "11111")
				.СПараметрами(" ", 3, "   ")
				.СПараметрами("_", 7, "_______")
				.СПараметрами("абв", 3, "абвабвабв")
		.ДобавитьТестовыйНабор("ЗатеретьПараметрПриЕгоОтсутствииВШаблоне")
			.ДобавитьТест("Тест_ЗатеретьПараметрПриЕгоОтсутствииВШаблоне")
				.СПараметрами("%1", 1, Ложь)
				.СПараметрами("  ", 1, Истина)
				.СПараметрами("%1%2", 2, Ложь)
				.СПараметрами("%1%2", 3, Истина)
				.СПараметрами("%2", 2, Ложь)
				.СПараметрами("%2", 1, Истина)
		.ДобавитьТестовыйНабор("СтрШаблонБезопасно")
			.ДобавитьТест("Тест_СтрШаблонБезопасно")
				.СПараметрами("%1", 1, 1)
				.СПараметрами("%1", 1, 10)
				.СПараметрами("%1 %2 %3 %4", 4, 10)
				.СПараметрами("%1 %2 %3 %4", 4, 1)
				.СПараметрами("%1 %2 %3 %4 %5 %6 %7 %8 %9 %10", 10, 10)
	;

КонецПроцедуры

#Область Тесты

#Область ВставитьПараметрыВСтроку

Процедура Тест_ВставитьПараметрыВСтроку_ПараметрыВнутри() Экспорт

	Перем ЮТест;
	ЮТест = ЖЧТ_Движок.ЮТест();

	ТестируемыйМодуль = ТестируемыйМодуль();
	
	ШаблонСтроки = "Привет, %Фамилия% %Имя% %Отчество% !";
	ЗначенияСтроки = Новый Структура("Фамилия,Имя,Отчество", "Иванов", "Иван", "Иванович");
	
	Результат = ТестируемыйМодуль.ВставитьПараметрыВСтроку(ШаблонСтроки, ЗначенияСтроки);
	
	ЮТест.ОжидаетЧто(Результат).Равно("Привет, Иванов Иван Иванович !");
	
КонецПроцедуры

Процедура Тест_ВставитьПараметрыВСтроку_ПараметрВНачале() Экспорт

	Перем ЮТест;
	ЮТест = ЖЧТ_Движок.ЮТест();

	ТестируемыйМодуль = ТестируемыйМодуль();
	
	ШаблонСтроки = "%Свойство% = %Значение% !";
	ЗначенияСтроки = Новый Структура("Свойство,Значение", "Сумма", 100);

	Результат = ТестируемыйМодуль.ВставитьПараметрыВСтроку(ШаблонСтроки, ЗначенияСтроки);
	
	ЮТест.ОжидаетЧто(Результат).Равно("Сумма = 100 !");
	
КонецПроцедуры

Процедура Тест_ВставитьПараметрыВСтроку_ПараметрВКонце() Экспорт

	Перем ЮТест;
	ЮТест = ЖЧТ_Движок.ЮТест();

	ТестируемыйМодуль = ТестируемыйМодуль();
	
	ШаблонСтроки = "Мое значение: %Значение%";
	ЗначенияСтроки = Новый Структура("Значение", 1);

	Результат = ТестируемыйМодуль.ВставитьПараметрыВСтроку(ШаблонСтроки, ЗначенияСтроки);
	
	ЮТест.ОжидаетЧто(Результат).Равно("Мое значение: 1");
	
КонецПроцедуры

Процедура Тест_ВставитьПараметрыВСтроку_ТолькоПараметр() Экспорт

	Перем ЮТест;
	ЮТест = ЖЧТ_Движок.ЮТест();

	ТестируемыйМодуль = ТестируемыйМодуль();
	
	ШаблонСтроки = "%Значение%";
	ЗначенияСтроки = Новый Структура("Значение", 1);

	Результат = ТестируемыйМодуль.ВставитьПараметрыВСтроку(ШаблонСтроки, ЗначенияСтроки);
	
	ЮТест.ОжидаетЧто(Результат).Равно("1");
	
КонецПроцедуры

Процедура Тест_ВставитьПараметрыВСтроку_Ошибка_НетСвойства() Экспорт

	Перем ЮТест;
	ЮТест = ЖЧТ_Движок.ЮТест();

	ТестируемыйМодуль = ТестируемыйМодуль();
	
	ШаблонСтроки = "У меня нет %Свойство1% !";
	ЗначенияСтроки = Новый Структура("Свойство2");

	ИмяМетода = "ВставитьПараметрыВСтроку";
	ПараметрыМетода = ЖЧТ_Движок.ЮТКоллекции().ЗначениеВМассиве(ШаблонСтроки, ЗначенияСтроки);
	
	ЮТест.ОжидаетЧто(ТестируемыйМодуль)
		.Метод(ИмяМетода, ПараметрыМетода)
		.ВыбрасываетИсключение("Поле объекта не обнаружено (Свойство1)");
	
КонецПроцедуры

Процедура Тест_ВставитьПараметрыВСтроку_Ошибка_НомерныеПараметры() Экспорт

	Перем ЮТест;
	ЮТест = ЖЧТ_Движок.ЮТест();

	ТестируемыйМодуль = ТестируемыйМодуль();
	
	ШаблонСтроки = "Текст с параметром %1!";
	ЗначенияСтроки = Новый Структура("С");

	ИмяМетода = "ВставитьПараметрыВСтроку";
	ПараметрыМетода = ЖЧТ_Движок.ЮТКоллекции().ЗначениеВМассиве(ШаблонСтроки, ЗначенияСтроки);
	
	ЮТест.ОжидаетЧто(ТестируемыйМодуль)
		.Метод(ИмяМетода, ПараметрыМетода)
		.ВыбрасываетИсключение("Поле объекта не обнаружено (1!)");
	
КонецПроцедуры

Процедура Тест_ВставитьПараметрыВСтроку_Ошибка_НеверныйФормат() Экспорт

	Перем ЮТест;
	ЮТест = ЖЧТ_Движок.ЮТест();

	ТестируемыйМодуль = ТестируемыйМодуль();
	
	ШаблонСтроки = "Неверный формат %% !";
	ЗначенияСтроки = Новый Структура("С");

	ИмяМетода = "ВставитьПараметрыВСтроку";
	ПараметрыМетода = ЖЧТ_Движок.ЮТКоллекции().ЗначениеВМассиве(ШаблонСтроки, ЗначенияСтроки);
	
	ЮТест.ОжидаетЧто(ТестируемыйМодуль)
		.Метод(ИмяМетода, ПараметрыМетода)
		.ВыбрасываетИсключение("Поле объекта не обнаружено");
	
КонецПроцедуры

#КонецОбласти

#Область СтрокаВыделеннаяОформлением

Процедура Тест_СтрокаВыделеннаяОформлением_Начало() Экспорт

	Перем ЮТест;
	ЮТест = ЖЧТ_Движок.ЮТест();

	ТестируемыйМодуль = ТестируемыйМодуль();
	
	Строка = "`Выделено` первое слово";
	Эталон = Новый ФорматированнаяСтрока(
		СтрНайтиИВыделитьОформлением("Выделено", "Выделено"),
		" первое слово");
		
	Результат = ТестируемыйМодуль.СтрокаВыделеннаяОформлением(Строка);
	
	ЮТест.ОжидаетЧто(Результат).Равно(Эталон);
	
КонецПроцедуры

Процедура Тест_СтрокаВыделеннаяОформлением_Середина() Экспорт

	Перем ЮТест;
	ЮТест = ЖЧТ_Движок.ЮТест();

	ТестируемыйМодуль = ТестируемыйМодуль();
	
	Строка = "Выделено `среднее` слово";
	Эталон = Новый ФорматированнаяСтрока(
		"Выделено ", 
		СтрНайтиИВыделитьОформлением("среднее", "среднее"),
		" слово");
		
	Результат = ТестируемыйМодуль.СтрокаВыделеннаяОформлением(Строка);
	
	ЮТест.ОжидаетЧто(Результат).Равно(Эталон);
	
КонецПроцедуры

Процедура Тест_СтрокаВыделеннаяОформлением_Конец() Экспорт

	Перем ЮТест;
	ЮТест = ЖЧТ_Движок.ЮТест();

	ТестируемыйМодуль = ТестируемыйМодуль();
	
	Строка = "Выделено последнее `слово`";
	Эталон = Новый ФорматированнаяСтрока(
		"Выделено последнее ", 
		СтрНайтиИВыделитьОформлением("слово", "слово"));
		
	Результат = ТестируемыйМодуль.СтрокаВыделеннаяОформлением(Строка);
	
	ЮТест.ОжидаетЧто(Результат).Равно(Эталон);
	
КонецПроцедуры

Процедура Тест_СтрокаВыделеннаяОформлением_Множество() Экспорт

	Перем ЮТест;
	ЮТест = ЖЧТ_Движок.ЮТест();

	ТестируемыйМодуль = ТестируемыйМодуль();
	
	Е = СтрНайтиИВыделитьОформлением("е", "е");
	О = СтрНайтиИВыделитьОформлением("о", "о");
	
	Строка = "В эт`о`й стр`о`к`е` выд`е`л`е`ны вс`е` буквы `е` и `о`";
	Эталон = Новый ФорматированнаяСтрока(
		"В эт", О, "й стр", О, "к", Е, " выд", Е, "л", Е, "ны вс", Е, " буквы ", Е, " и ", О
	);
	
	Результат = ТестируемыйМодуль.СтрокаВыделеннаяОформлением(Строка);
	
	ЮТест.ОжидаетЧто(Результат).Равно(Эталон);
	
КонецПроцедуры

#КонецОбласти

#Область СтрокаНайтиИВыделитьОформлением

Процедура Тест_СтрокаНайтиИВыделитьОформлением(Строка, ИскомаяСтрока, Знач Эталон) Экспорт

	Перем ЮТест;
	ЮТест = ЖЧТ_Движок.ЮТест();

	ТестируемыйМодуль = ТестируемыйМодуль();
	
	Если ЗначениеЗаполнено(Эталон) Тогда
		Эталон = ТестируемыйМодуль.СтрокаВыделеннаяОформлением(Эталон);
	КонецЕсли;
	
	Результат = ТестируемыйМодуль.СтрокаНайтиИВыделитьОформлением(Строка, ИскомаяСтрока);
	
	ЮТест.ОжидаетЧто(Результат).Равно(Эталон);
	
КонецПроцедуры

#КонецОбласти

#Область ШаблонСтрокиКорректен

Процедура Тест_ШаблонСтрокиКорректен(ШаблонСтроки, ШаблонКорректен, ПодсказкаПроверки) Экспорт

	Перем ЮТест;
	ЮТест = ЖЧТ_Движок.ЮТест();

	ТестируемыйМодуль = ТестируемыйМодуль();
	
	Результат = ТестируемыйМодуль.ШаблонСтрокиКорректен(ШаблонСтроки);
	ЮТест.ОжидаетЧто(Результат, ПодсказкаПроверки).Равно(ШаблонКорректен);
	
КонецПроцедуры

#КонецОбласти

#Область СтрокаСимволов

Процедура Тест_СтрокаСимволов(Символ, Длина, ОжидаемыйРезультат) Экспорт
	 
	Перем ЮТест;
	ЮТест = ЖЧТ_Движок.ЮТест();

	ТестируемыйМодуль = ТестируемыйМодуль();
	
	Результат = ТестируемыйМодуль.СтрокаСимволов(Длина, Символ);
	ЮТест.ОжидаетЧто(Результат).Равно(ОжидаемыйРезультат);
	
КонецПроцедуры

#КонецОбласти

#Область ЗатеретьПараметрПриЕгоОтсутствииВШаблоне

Процедура Тест_ЗатеретьПараметрПриЕгоОтсутствииВШаблоне(Шаблон, Номер, Затереть) Экспорт
	 
	Перем ЮТест;
	ЮТест = ЖЧТ_Движок.ЮТест();

	Параметр = Новый УникальныйИдентификатор();
	Ожидание = ?(Затереть, Неопределено, Параметр);
	
	ТестируемыйМодуль = ТестируемыйМодуль();
	ТестируемыйМодуль.ЗатеретьПараметрПриЕгоОтсутствииВШаблоне(Шаблон, Параметр, Номер);
	
	ЮТест.ОжидаетЧто(Параметр).Равно(Ожидание);
	
КонецПроцедуры

#КонецОбласти

#Область СтрШаблонБезопасно

Процедура Тест_СтрШаблонБезопасно(Шаблон, ЧислоПараметровВШаблоне, ЧислоПередаваемыхПараметров) Экспорт
	 
	Перем ЮТест;
	ЮТест = ЖЧТ_Движок.ЮТест();
	
	Параметры1  = ТестовыеДанные_СтрШаблонБезопасно_Параметры(1 , ЧислоПараметровВШаблоне, ЧислоПередаваемыхПараметров);
	Параметры2  = ТестовыеДанные_СтрШаблонБезопасно_Параметры(2 , ЧислоПараметровВШаблоне, ЧислоПередаваемыхПараметров);
	Параметры3  = ТестовыеДанные_СтрШаблонБезопасно_Параметры(3 , ЧислоПараметровВШаблоне, ЧислоПередаваемыхПараметров);
	Параметры4  = ТестовыеДанные_СтрШаблонБезопасно_Параметры(4 , ЧислоПараметровВШаблоне, ЧислоПередаваемыхПараметров);
	Параметры5  = ТестовыеДанные_СтрШаблонБезопасно_Параметры(5 , ЧислоПараметровВШаблоне, ЧислоПередаваемыхПараметров);
	Параметры6  = ТестовыеДанные_СтрШаблонБезопасно_Параметры(6 , ЧислоПараметровВШаблоне, ЧислоПередаваемыхПараметров);
	Параметры7  = ТестовыеДанные_СтрШаблонБезопасно_Параметры(7 , ЧислоПараметровВШаблоне, ЧислоПередаваемыхПараметров);
	Параметры8  = ТестовыеДанные_СтрШаблонБезопасно_Параметры(8 , ЧислоПараметровВШаблоне, ЧислоПередаваемыхПараметров);
	Параметры9  = ТестовыеДанные_СтрШаблонБезопасно_Параметры(9 , ЧислоПараметровВШаблоне, ЧислоПередаваемыхПараметров);
	Параметры10 = ТестовыеДанные_СтрШаблонБезопасно_Параметры(10, ЧислоПараметровВШаблоне, ЧислоПередаваемыхПараметров);
	
	ТестируемыйМодуль = ТестируемыйМодуль();
	
	Результат = ТестируемыйМодуль.СтрШаблонБезопасно(Шаблон, Параметры1.Передаваемый, 
		Параметры2.Передаваемый, Параметры3.Передаваемый, Параметры4.Передаваемый, 
		Параметры5.Передаваемый, Параметры6.Передаваемый, Параметры7.Передаваемый, 
		Параметры8.Передаваемый, Параметры9.Передаваемый, Параметры10.Передаваемый);
	
	Ожидание = СтрШаблон(Шаблон, Параметры1.Проверяемый, 
		Параметры2.Проверяемый, Параметры3.Проверяемый, Параметры4.Проверяемый, 
		Параметры5.Проверяемый, Параметры6.Проверяемый, Параметры7.Проверяемый, 
		Параметры8.Проверяемый, Параметры9.Проверяемый, Параметры10.Проверяемый);
	
	ЮТест.ОжидаетЧто(Результат).Равно(Ожидание);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ТестовыеДанные

#Область СтрШаблонБезопасно

Функция ТестовыеДанные_СтрШаблонБезопасно_Параметры(Номер, ЧислоПараметровВШаблоне, ЧислоПередаваемыхПараметров)
	
	Значение = Новый УникальныйИдентификатор();
	
	Результат = Новый Структура;
	Результат.Вставить("Передаваемый", 
		ТестовыеДанные_СтрШаблонБезопасно_Параметр(Номер, ЧислоПередаваемыхПараметров, Значение));
	Результат.Вставить("Проверяемый", 
		ТестовыеДанные_СтрШаблонБезопасно_Параметр(
			Номер, Мин(ЧислоПараметровВШаблоне, ЧислоПередаваемыхПараметров), Значение));
			
	Возврат Результат;
	
КонецФункции

Функция ТестовыеДанные_СтрШаблонБезопасно_Параметр(Номер, ЧислоПараметров, Значение)
	
	Если Номер > ЧислоПараметров Тогда
		Возврат Неопределено;
	Иначе
		Возврат Значение;
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область Общее

// Тестируемый модуль.
// 
// Возвращаемое значение:
//  ОбщийМодуль
Функция ТестируемыйМодуль() Экспорт

	Возврат ЖЧТ_Модули.СтроковыеФункции();
	
КонецФункции

#КонецОбласти

#КонецОбласти