
#Область СлужебныйПрограммныйИнтерфейс

// Регистрация тестов
Процедура ИсполняемыеСценарии() Экспорт

	Перем ЮТТесты;
	ЮТТесты = ЖЧТ_Движок.ЮТТесты();
	
	ЮТТесты
		.ДобавитьТестовыйНабор("НайтиСсылкуПоРеквизиту")
			.ДобавитьТест("Тест_НайтиСсылкуПоРеквизиту_Успешно").ВТранзакции()
			.ДобавитьТест("Тест_НайтиСсылкуПоРеквизиту_Пусто")
			.ДобавитьТест("Тест_НайтиСсылкуПоРеквизиту_Ошибка_Метаданные")
			.ДобавитьТест("Тест_НайтиСсылкуПоРеквизиту_Ошибка_Реквизит")
			.ДобавитьТест("Тест_НайтиСсылкуПоРеквизиту_Ошибка_Значение")
			
	;

КонецПроцедуры

#Область Тесты

#Область НайтиСсылкуПоРеквизиту

Процедура Тест_НайтиСсылкуПоРеквизиту_Успешно() Экспорт

	Перем ЮТест;
	ЮТест = ЖЧТ_Движок.ЮТест();

	ТестируемыйМодуль = ТестируемыйМодуль();
	
	ТекущиеМетаданные = Метаданные.Справочники.ЖЧТ_ТестовыйСправочник;
	
	Реквизиты = Новый Структура;
	Реквизиты.Вставить("Номер", 999);
	НовыйЭлемент = ЮТест.Данные().СоздатьЭлемент(ТекущиеМетаданные, "Тест", Реквизиты);
	
	НайденныйЭлемент = ТестируемыйМодуль.НайтиСсылкуПоРеквизиту(ТекущиеМетаданные, "Номер", Реквизиты.Номер);
	
	ЮТест.ОжидаетЧто(НайденныйЭлемент).Равно(НовыйЭлемент);
	
КонецПроцедуры

Процедура Тест_НайтиСсылкуПоРеквизиту_Пусто() Экспорт

	Перем ЮТест;
	ЮТест = ЖЧТ_Движок.ЮТест();

	ТестируемыйМодуль = ТестируемыйМодуль();
	
	ТекущиеМетаданные = Метаданные.Справочники.ЖЧТ_ТестовыйСправочник;
	
	НайденныйЭлемент = ТестируемыйМодуль.НайтиСсылкуПоРеквизиту(ТекущиеМетаданные, "Номер", 69);
	
	ЮТест.ОжидаетЧто(НайденныйЭлемент)
		.Равно(ТекущиеМетаданные.СтандартныеРеквизиты.Ссылка.Тип.ПривестиЗначение());
	
КонецПроцедуры

Процедура Тест_НайтиСсылкуПоРеквизиту_Ошибка_Метаданные() Экспорт

	Перем ЮТест, ЮТОбщий;
	ЮТест = ЖЧТ_Движок.ЮТест();
	ЮТОбщий = ЖЧТ_Движок.ЮТОбщий();

	ЮТест.ОжидаетЧто(ТестируемыйМодуль())
		.Метод("НайтиСсылкуПоРеквизиту", 
			ЮТОбщий.ЗначениеВМассиве(Неопределено, "", 0))
  		.ВыбрасываетИсключение("Значение не является значением объектного типа (ПолноеИмя)");
	
КонецПроцедуры

Процедура Тест_НайтиСсылкуПоРеквизиту_Ошибка_Реквизит() Экспорт

	Перем ЮТест, ЮТОбщий;
	ЮТест = ЖЧТ_Движок.ЮТест();
	ЮТОбщий = ЖЧТ_Движок.ЮТОбщий();

	ЮТест.ОжидаетЧто(ТестируемыйМодуль())
		.Метод("НайтиСсылкуПоРеквизиту", 
			ЮТОбщий.ЗначениеВМассиве(Метаданные.Справочники.ЖЧТ_ТестовыйСправочник, "ФейковыйРеквизит", 0))
  		.ВыбрасываетИсключение("Поле не найдено ""ФейковыйРеквизит""");
	
КонецПроцедуры

Процедура Тест_НайтиСсылкуПоРеквизиту_Ошибка_Значение() Экспорт

	Перем ЮТест, ЮТОбщий;
	ЮТест = ЖЧТ_Движок.ЮТест();
	ЮТОбщий = ЖЧТ_Движок.ЮТОбщий();

	ЮТест.ОжидаетЧто(ТестируемыйМодуль())
		.Метод("НайтиСсылкуПоРеквизиту", 
			ЮТОбщий.ЗначениеВМассиве(Метаданные.Справочники.ЖЧТ_ТестовыйСправочник, "Номер", Новый Массив))
  		.ВыбрасываетИсключение("Ошибка при вызове метода контекста (Выполнить)");
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Тестируемый модуль.
// 
// Возвращаемое значение:
//  ОбщийМодуль
Функция ТестируемыйМодуль() Экспорт

	Возврат ЖЧТ_Модули.РаботаСДанными();
	
КонецФункции

#КонецОбласти
