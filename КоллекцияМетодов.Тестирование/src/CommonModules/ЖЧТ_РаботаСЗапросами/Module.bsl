
#Область СлужебныйПрограммныйИнтерфейс

// Регистрация тестов
Процедура ИсполняемыеСценарии() Экспорт

	Перем ЮТТесты;
	ЮТТесты = ЖЧТ_Движок.ЮТТесты();
	
	ЮТТесты
		.ДобавитьТестовыйНабор("ЗаполнитьПараметрыЗапроса")
			.ДобавитьТест("Тест_ЗаполнитьПараметрыЗапроса")
		.ДобавитьТестовыйНабор("ДобавитьТаблицуВМенеджерВременныхТаблиц")
			.ДобавитьТест("Тест_ДобавитьТаблицуВМенеджерВременныхТаблиц")
		.ДобавитьТестовыйНабор("ОтформатированныйТекстЗапроса")
			.ДобавитьТест("Тест_ОтформатированныйТекстЗапроса")
		.ДобавитьТестовыйНабор("ПустаяТаблицаПоТекстуЗапроса")
			.ДобавитьТест("Тест_ПустаяТаблицаПоТекстуЗапроса")
				.СПараметрами("ВЫБРАТЬ 1 КАК Число, ДАТАВРЕМЯ(1,1,1) КАК Дата, """" КАК Строка")
				.СПараметрами("ВЫБРАТЬ 1 КАК Число ПОМЕСТИТЬ ВТ; ВЫБРАТЬ Число ИЗ ВТ")
				.СПараметрами("ВЫБРАТЬ 1 КАК Число ПОМЕСТИТЬ ВТ; ВЫБРАТЬ Число ИЗ ВТ; УНИЧТОЖИТЬ ВТ")
		.ДобавитьТестовыйНабор("Построитель_ДобавитьОтбор")
			.ДобавитьТест("Тест_Построитель_ДобавитьОтбор_Значение")
			.ДобавитьТест("Тест_Построитель_ДобавитьОтбор_Списки")
		
	;

КонецПроцедуры

#Область Тесты

#Область ЗаполнитьПараметрыЗапроса

Процедура Тест_ЗаполнитьПараметрыЗапроса() Экспорт

	Перем ЮТест;
	ЮТест = ЖЧТ_Движок.ЮТест();

	ТестируемыйМодуль = ТестируемыйМодуль();
	
	Запрос = Новый Запрос("ВЫБРАТЬ &П1, &П2, &П4");
	Параметры = Новый Структура("П1,П3,П4,П5");
	
	ТестируемыйМодуль.ЗаполнитьПараметрыЗапроса(Запрос, Параметры);
	
	ЮТест.ОжидаетЧто(Запрос.Параметры)
		.ИмеетСвойство("П1")
		.НеИмеетСвойства("П2")
		.НеИмеетСвойства("П3")
		.ИмеетСвойство("П4")
		.НеИмеетСвойства("П5");
	
КонецПроцедуры

#КонецОбласти

#Область ДобавитьТаблицуВМенеджерВременныхТаблиц

Процедура Тест_ДобавитьТаблицуВМенеджерВременныхТаблиц() Экспорт

	Перем ЮТест;
	ЮТест = ЖЧТ_Движок.ЮТест();

	ТестируемыйМодуль = ТестируемыйМодуль();
	
	МВТ = Новый МенеджерВременныхТаблиц();
	ТЗ = Новый ТаблицаЗначений();
	ТЗ.Колонки.Добавить("К1", Новый ОписаниеТипов("Число"));
	ТЗ.Колонки.Добавить("К2", Новый ОписаниеТипов("Строка"));
	ТЗ.Колонки.Добавить("К3", Новый ОписаниеТипов("Дата"));
	
	ИмяТаблицы = "Тест";
	ТестируемыйМодуль.ДобавитьТаблицуВМенеджерВременныхТаблиц(МВТ, "Тест", ТЗ);
	
	ВТ = МВТ.Таблицы.Найти("Тест");
	ЮТест.ОжидаетЧто(ВТ).ИмеетТип(Тип("ВременнаяТаблицаЗапроса"))
		.Свойство("ПолноеИмя").Равно(ИмяТаблицы)
		.Свойство("Колонки").ИмеетДлину(ТЗ.Колонки.Количество());
	
	Для Каждого Колонка Из ВТ.Колонки Цикл
		ЮТест.ОжидаетЧто(Колонка.ТипЗначения).Равно(ТЗ.Колонки[Колонка.Имя].ТипЗначения);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОтформатированныйТекстЗапроса

Процедура Тест_ОтформатированныйТекстЗапроса() Экспорт

	Перем ЮТест;
	ЮТест = ЖЧТ_Движок.ЮТест();

	ИзначальныйТекст = "ВЫБРАТЬ 1 КАК Поле ПОМЕСТИТЬ ВТ; ВЫБРАТЬ ПОЛЕ ИЗ ВТ";
	ОжидаемыйТекст   = "ВЫБРАТЬ
	|	1 КАК Поле
	|ПОМЕСТИТЬ ВТ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ.Поле КАК Поле
	|ИЗ
	|	ВТ КАК ВТ";

	ТестируемыйМодуль = ТестируемыйМодуль();
	Результат = ТестируемыйМодуль.ОтформатированныйТекстЗапроса(ИзначальныйТекст);

	ЮТест.ОжидаетЧто(Результат).Равно(ОжидаемыйТекст);
	
КонецПроцедуры

#КонецОбласти

#Область ПустаяТаблицаПоТекстуЗапроса

Процедура Тест_ПустаяТаблицаПоТекстуЗапроса(ТекстЗапроса) Экспорт

	Перем ЮТест;
	ЮТест = ЖЧТ_Движок.ЮТест();

	ТекстЗапроса = "ВЫБРАТЬ 1 КАК Число, ДАТАВРЕМЯ(1,1,1) КАК Дата, """" КАК Строка";

	ТестируемыйМодуль = ТестируемыйМодуль();
	Результат = ТестируемыйМодуль.ПустаяТаблицаПоТекстуЗапроса(ТекстЗапроса);

	Запрос = Новый Запрос(ТекстЗапроса);
	Эталон = Запрос.Выполнить().Выгрузить();
	Эталон.Очистить();

	ЮТест.ОжидаетЧто(Результат)
		.ИмеетТип("ТаблицаЗначений")
		.Равно(Эталон);
	
КонецПроцедуры

#КонецОбласти

#Область Построитель_ДобавитьОтбор

Процедура Тест_Построитель_ДобавитьОтбор_Значение() Экспорт

	Перем ЮТест;
	ЮТест = ЖЧТ_Движок.ЮТест();

	ТестируемыйМодуль = ТестируемыйМодуль();
	
	ТекстЗапроса = "ВЫБРАТЬ Ссылка ИЗ Справочник.ЖЧТ_ТестовыйСправочник";
	Построитель = Новый ПостроительЗапроса(ТекстЗапроса);
	Построитель.ЗаполнитьНастройки();
	
	ФейковаяСсылка = ЖЧТ_Общее.ФейковаяСсылка("СправочникСсылка.ЖЧТ_ТестовыйСправочник");
	
	Результат = ТестируемыйМодуль.Построитель_ДобавитьОтбор(Построитель, "Ссылка", ФейковаяСсылка);

	ЮТест.ОжидаетЧто(Результат)
		.ИмеетТип("ЭлементОтбора")
		.Свойство("Использование").ЭтоИстина()
		.Свойство("ВидСравнения").Равно(ВидСравнения.Равно)
		.Свойство("Значение").Равно(ФейковаяСсылка)
	;
	
КонецПроцедуры

Процедура Тест_Построитель_ДобавитьОтбор_Списки() Экспорт

	Перем ЮТест;
	ЮТест = ЖЧТ_Движок.ЮТест();

	ТестируемыйМодуль = ТестируемыйМодуль();
	
	ТекстЗапроса = "ВЫБРАТЬ Ссылка ИЗ Справочник.ЖЧТ_ТестовыйСправочник";
	Построитель = Новый ПостроительЗапроса(ТекстЗапроса);
	Построитель.ЗаполнитьНастройки();
	
	Ссылка1 = ЖЧТ_Общее.ФейковаяСсылка("СправочникСсылка.ЖЧТ_ТестовыйСправочник");
	Ссылка2 = ЖЧТ_Общее.ФейковаяСсылка("СправочникСсылка.ЖЧТ_ТестовыйСправочник");
	
	МассивСсылок = Новый Массив;
	МассивСсылок.Добавить(Ссылка1);
	МассивСсылок.Добавить(Ссылка2);
	
	ФиксированныйМассив = Новый ФиксированныйМассив(МассивСсылок);
	
	СписокСсылок = Новый СписокЗначений();
	СписокСсылок.ЗагрузитьЗначения(МассивСсылок);
	
	ВариантыСписков = Новый Массив;
	ВариантыСписков.Добавить(МассивСсылок);
	ВариантыСписков.Добавить(ФиксированныйМассив);
	ВариантыСписков.Добавить(СписокСсылок);
	
	Для Каждого ВариантСписка Из ВариантыСписков Цикл
		
		Результат = ТестируемыйМодуль.Построитель_ДобавитьОтбор(Построитель, "Ссылка", ВариантСписка);
	
		ЮТест.ОжидаетЧто(Результат)
			.ИмеетТип("ЭлементОтбора")
			.Свойство("Использование").ЭтоИстина()
			.Свойство("ВидСравнения").Равно(ВидСравнения.ВСписке)
			.Свойство("Значение").ИмеетТип("СписокЗначений")
			.Свойство("Значение[0].Значение").Равно(Ссылка1)
			.Свойство("Значение[1].Значение").Равно(Ссылка2)
		;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Тестируемый модуль.
// 
// Возвращаемое значение:
//  ОбщийМодуль
Функция ТестируемыйМодуль() Экспорт

	Возврат ЖЧТ_Модули.РаботаСЗапросами();
	
КонецФункции

#КонецОбласти
